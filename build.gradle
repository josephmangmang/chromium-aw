// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.google.protobuf'

group = 'com.github.ridi'
version = '93.0.4577.82-1'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkVersion '21.1.6352462'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
        versionCode 1
        versionName version
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "androidx.asynclayoutinflater:asynclayoutinflater:1.0.0"
    implementation 'androidx.core:core:1.5.0'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation "com.google.android.gms:play-services-gcm:17.0.0"
    implementation "com.google.android.gms:play-services-location:18.0.0"
    implementation "com.google.android.gms:play-services-vision:20.1.3"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.google.protobuf:protobuf-javalite:3.11.0"
    implementation "org.glassfish:javax.annotation:10.0-b28"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The 3-Clause BSD License'
                    url 'https://opensource.org/licenses/BSD-3-Clause'
                    distribution 'repo'
                }
            }
        }
    }
}
